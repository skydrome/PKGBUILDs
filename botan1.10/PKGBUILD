# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: drakkan <nicola.murino@gmail.com>

pkgname=botan1.10
pkgver=1.10.17
pkgrel=1
pkgdesc='Crypto library written in C++ - old stable branch'
license=('BSD')
arch=('x86_64' 'i686')
url='https://botan.randombit.net'
makedepends=('python3')
source=("https://botan.randombit.net/releases/old/Botan-${pkgver}.tgz"{,.asc})
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')  # Botan Distribution Key
sha256sums=('6847ffb64b8d2f939dccfecc17bd2c80385d08f7621e2c56d3a335118e823613'
            'SKIP')

build() {
    cd "Botan-$pkgver"

    python3 configure.py \
        --prefix=/usr --cc=gcc \
        --enable-modules=cvc,bzip2,zlib \
        --disable-modules=gnump

    make CXX="g++ $CXXFLAGS -std=c++11"
}

package() {
    cd "Botan-$pkgver"

    make DESTDIR="$pkgdir/usr" install
    find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
    install -Dm644 doc/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
